---
page_title: "octopusdeploy_process_child_steps_order Resource - terraform-provider-octopusdeploy"
subcategory: "Runbook & Deployment Processes"
description: |-
  This resource manages order of Child Steps in a Runbook or Deployment Process in Octopus Deploy.
---

# octopusdeploy_process_child_steps_order (Resource)

This resource manages order of Child Steps in a Runbook or Deployment Process in Octopus Deploy.

### Remarks
Child Step Order explicitly controls the order of Child Steps within a Parent Step.

The Parent Step will always be the first action in the group, followed by the child steps in the order declared in the `children` attribute.

## Example Usage

```terraform
# Deployment Process with a Child Step and explicit Child Step Order
resource "octopusdeploy_environment" "development" {
  name = "Development"
}

resource "octopusdeploy_environment" "production" {
  name = "Production"
}

resource "octopusdeploy_project" "example" {
  project_group_id = "ProjectGroups-1"
  lifecycle_id = "Lifecycles-1"
  name = "Example"
}

resource "octopusdeploy_channel" "example" {
  name       = "Example Channel"
  project_id = octopusdeploy_project.example.id
}

resource "octopusdeploy_process" "example" {
  project_id  = octopusdeploy_project.example.id
}

resource "octopusdeploy_process_step" "example" {
  process_id  = octopusdeploy_process.example.id
  name = "Example Step"
  type = "Octopus.Script"
  properties = {
    "Octopus.Action.TargetRoles" = "role-1"
  }
  execution_properties = {
    "Octopus.Action.Script.ScriptBody" = "Write-Host 'Example Step...'"
  }
}

resource "octopusdeploy_process_child_step" "child_one" {
  process_id  = octopusdeploy_process.example.id
  parent_id = octopusdeploy_process_step.example.id
  name = "Child One"
  type = "Octopus.Script"
  execution_properties = {
    "Octopus.Action.Script.ScriptBody" = "Write-Host 'Child 1...'"
  }
}

resource "octopusdeploy_process_child_step" "child_two" {
  process_id  = octopusdeploy_process.example.id
  parent_id = octopusdeploy_process_step.example.id
  name = "Child Two"
  type = "Octopus.Script"
  execution_properties = {
    "Octopus.Action.Script.ScriptBody" = "Write-Host 'Child 3...'"
  }
}

resource "octopusdeploy_process_child_steps_order" "example" {
  process_id = octopusdeploy_process.example.id
  parent_id = octopusdeploy_process_step.example.id
  children = [
    octopusdeploy_process_child_step.child_one.id,
    octopusdeploy_process_child_step.child_two.id,
  ]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `children` (List of String) Child steps in the order of execution
- `parent_id` (String) Id of the process step children belong to.
- `process_id` (String) Id of the process parent step belongs to.

### Optional

- `space_id` (String) The space ID associated with this process_child_steps_order.

### Read-Only

- `id` (String) The unique ID for this resource.

## Import

Import is supported using the following syntax:

```shell
terraform import [options] octopusdeploy_process_child_steps_order.<name> "<process-id>:<parent-step-id>"
```
