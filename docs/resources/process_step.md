---
page_title: "octopusdeploy_process_step Resource - terraform-provider-octopusdeploy"
subcategory: "Runbook & Deployment Processes"
description: |-
  This resource manages a single step of a Runbook or Deployment Process in Octopus Deploy.
---

# octopusdeploy_process_step (Resource)

This resource manages a single step of a Runbook or Deployment Process in Octopus Deploy.

### Remarks
A Step is the building block of a Deployment or Runbook Process. 

When there are multiple Steps in a Process, we strongly recommend adding a `octopusdeploy_process_step_order` resource to pin the step order. If you later need to change the order of steps in your process, or insert a new step within an existing process, you'll need the Step Order defined first. Without an explicit Step Order, Steps will be added to the process in the order they're applied by Terraform - this is usually the order they appear in your HCL, but is not guaranteed to be deterministic. 

Unlike the old `octopusdeploy_deployment_process` resource, we do not currently support "strongly-typed" deployment actions. We found that the combination of strongly-typed properties along with the key-value-pair `properties` collection was a source of a lot of state drift, which the new approach is designed to prevent. We know that this makes it more laborious to author a functional Process in a code-first manner, and plan to address the problem in the future. The new shape of the `octopusdeploy_process` and associated resources will actually better support strongly-typed resources in the future without having the same susceptibility to ordering problems, but we want to be able to generate these so that they stay in sync as Octopus changes over time, rather than hand-crafting them as we previously were. For now, we have written a guide to show you how to discover the correct property combinations for each step.

This resource also contains a concept that doesn't exist in the Octopus Deploy domain model: `properties` vs `execution_properties`:

* `properties` are the inputs to the step itself
* `execution_properties` are the controls for _how_ the step runs, particularly relevant in parent/child step situations

## Example Usage

```terraform
resource "octopusdeploy_environment" "development" {
  name = "Development"
}

resource "octopusdeploy_environment" "production" {
  name = "Production"
}

resource "octopusdeploy_project" "example" {
  project_group_id = "ProjectGroups-1"
  lifecycle_id = "Lifecycles-1"
  name = "Example"
}

resource "octopusdeploy_channel" "example" {
  name       = "Example Channel (OK to Delete)"
  project_id = octopusdeploy_project.example.id
}

resource "octopusdeploy_process" "example" {
  project_id  = octopusdeploy_project.example.id
}

# Run script step
resource "octopusdeploy_process_step" "run_script" {
  process_id  = octopusdeploy_process.example.id
  name = "Run My Script"
  properties = {
    "Octopus.Action.MaxParallelism" = "2"
    "Octopus.Action.TargetRoles" = "role-1,role-2"
  }
  type = "Octopus.Script"
  environments = [octopusdeploy_environment.development.id]
  excluded_environments = [octopusdeploy_environment.production.id]
  channels = [octopusdeploy_channel.example.id]
  notes = "Script example"
  execution_properties = {
    "Octopus.Action.RunOnServer" = "True"
    "Octopus.Action.Script.ScriptSource" = "Inline"
    "Octopus.Action.Script.Syntax"       = "PowerShell"
    "Octopus.Action.Script.ScriptBody" = <<-EOT
      Write-Host "Executing step..."
    EOT
  }
}

# Manual intervention
resource "octopusdeploy_process_step" "approval" {
  process_id  = octopusdeploy_process.example.id
  name = "Approve deployment"
  type = "Octopus.Manual"
  execution_properties = {
    "Octopus.Action.RunOnServer" = "True"
    "Octopus.Action.Manual.Instructions" = "Example of manual blocking step"
    "Octopus.Action.Manual.BlockConcurrentDeployments" = "True"
    "Octopus.Action.Manual.ResponsibleTeamIds" = "teams-managers"
  }
}

# Package deployment with primary package
resource "octopusdeploy_process_step" "deploy_package" {
  process_id  = octopusdeploy_process.example.id
  name = "Package deployment"
  properties = {
    "Octopus.Action.TargetRoles" = "role-one"
  }
  type = "Octopus.TentaclePackage"
  packages = {
    "": {
      package_id: "my.package"
      feed_id: "Feeds-1"
    }
  }
  execution_properties = {
    "Octopus.Action.RunOnServer" = "True"    
    # Reference primary package in execution properties for legacy purposes
    "Octopus.Action.Package.DownloadOnTentacle" = "False"
    "Octopus.Action.Package.FeedId" = "Feeds-1"
    "Octopus.Action.Package.PackageId" = "my.package"
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `name` (String) The name of this resource.
- `process_id` (String) Id of the process this step belongs to.
- `type` (String) Execution type of the step.

### Optional

- `channels` (Set of String) A set of channels associated with this step.
- `condition` (String) When to run the step, one of 'Success', 'Failure', 'Always' or 'Variable'
- `container` (Attributes) When set used to run step inside a container on the Octopus Server. Octopus Server must support container execution. (see [below for nested schema](#nestedatt--container))
- `environments` (Set of String) A set of environments within which this step will run.
- `excluded_environments` (Set of String) A set of environments that this step will be skipped in.
- `execution_properties` (Map of String) A collection of step action properties where the key is the property name and the value is its value.
- `git_dependencies` (Attributes Map) References of git dependencies for this step where key is a name of the reference (can be empty). Is the Git equivalent of packages (see [below for nested schema](#nestedatt--git_dependencies))
- `is_disabled` (Boolean) Indicates the disabled status of this step.
- `is_required` (Boolean) Indicates the required status of this step.
- `notes` (String) The notes associated with this step.
- `package_requirement` (String) Whether to run this step before or after package acquisition (if possible).
- `packages` (Attributes Map) Package references associated with this step where key is a name of the package reference (use empty name for primary package) (see [below for nested schema](#nestedatt--packages))
- `properties` (Map of String) A collection of process step properties where the key is the property name and the value is its value.
- `slug` (String) The human-readable unique identifier for the step.
- `space_id` (String) The space ID associated with this process_step.
- `start_trigger` (String) Whether to run this step after the previous step ('StartAfterPrevious') or at the same time as the previous step ('StartWithPrevious').
- `tenant_tags` (Set of String) A set of tenant tags associated with this step.
- `worker_pool_id` (String) The worker pool associated with this step.
- `worker_pool_variable` (String) The worker pool variable associated with this step.

### Read-Only

- `id` (String) The unique ID for this resource.

<a id="nestedatt--container"></a>
### Nested Schema for `container`

Optional:

- `feed_id` (String) Feed where the container will be pulled from.
- `image` (String) Image of the container with tag included.


<a id="nestedatt--git_dependencies"></a>
### Nested Schema for `git_dependencies`

Required:

- `default_branch` (String) Name of the default branch of the repository.
- `git_credential_type` (String) The Git credential authentication type.
- `repository_uri` (String) The Git URI for the repository where this resource is sourced from

Optional:

- `file_path_filters` (Set of String) List of file path filters used to narrow down the directory where files are to be sourced from. Supports glob patten syntax.
- `git_credential_id` (String) ID of an existing Git credential.


<a id="nestedatt--packages"></a>
### Nested Schema for `packages`

Required:

- `package_id` (String) Package ID or a variable-expression

Optional:

- `acquisition_location` (String) Whether to acquire this package on the server ('Server'), target ('ExecutionTarget') or not at all ('NotAcquired'). Can be an expression
- `feed_id` (String) The feed ID associated with this package reference
- `properties` (Map of String) A collection of properties associated with this package

Read-Only:

- `id` (String) The unique ID for this resource.

## Import

Import is supported using the following syntax:

```shell
terraform import [options] octopusdeploy_process_step.<name> "<process-id>:<step-id>"
```
